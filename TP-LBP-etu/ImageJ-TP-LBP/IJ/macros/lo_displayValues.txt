// These macros demonstrate how to use the
// getSelectionCoordinates function.

macro "Display XY Coordinates" {
	requires("1.41d"); // setFont() added in 1.33j
	getSelectionBounds(x_ul, y_ul, width, height);
	values="";
	if (bitDepth()==24) {
		for (y=y_ul; y<y_ul+height; y++) {
			for (x=x_ul; x<x_ul+width; x++) {
				v = getPixel(x,y);
				red = (v>>16)&0xff;  // extract red byte (bits 23-17)
				green = (v>>8)&0xff; // extract green byte (bits 15-8)
				blue = v&0xff;       // extract blue byte (bits 7-0)
				rgb_values = ""+left_pad(''+red,3)+','+left_pad(green,3)+','+left_pad(blue,3);
				values=values+left_pad(rgb_values,13);
			}			
			values=values+"\n";
		}

	} else {
		for (y=y_ul; y<y_ul+height; y++) {
			for (x=x_ul; x<x_ul+width; x++) {
				v = getPixel(x,y);
				values=values+left_pad(v,4);
			}			
			values=values+"\n";
		}
	}
	setColor(0, 0, 0);
	font_height = 20;
	setFont("Courier new", font_height);
	setJustification("left");
	values_first_row = substring(values,0,indexOf(values, '\n'));
	newImage("temp", "RGB white", getStringWidth(values_first_row), (y-y_ul)*font_height*1.2, 1);
	drawString(""+values, 0, font_height+2);
}

// pads a string to total_length adding head spaces
function left_pad (a_string, total_length) {
	spaces=""; 
	string_length=lengthOf(""+a_string);
	for (sp=0; sp<total_length-string_length; sp++) spaces=spaces+" ";
	result = spaces+a_string;
	return result;	// Pas de parenthèses, sinon résultat converti en numérique !!
}

macro "List XY Coordinates" {
	requires("1.30k");
	getSelectionBounds(x_ul, y_ul, width, height);
	for (y=y_ul; y<y_ul+height; y++)
		for (x=x_ul; x<x_ul+height; x++) {
			v = getPixel(x,y);
			row = nResults;
			setResult("X", row, x);
			setResult("Y", row, y);
			if (nSlices>1) setResult("Z", row, z);
			if (bitDepth==24) {
				red = (v>>16)&0xff;  // extract red byte (bits 23-17)
				green = (v>>8)&0xff; // extract green byte (bits 15-8)
				blue = v&0xff;       // extract blue byte (bits 7-0)
				setResult("Red", row, red);
				setResult("Green", row, green);
				setResult("Blue", row, blue);
			} else
				setResult("Value", row, v);
		}
	updateResults;
}




